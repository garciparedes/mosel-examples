
model ModelName
    uses "mmxprs"; !gain access to the Xpress-Optimizer solver
    
    declarations
        m = 3
        n = 6
        sectores = 1..n
        barcos = 1..m
        costo, tiempo: array(barcos, sectores) of integer
        b: array(barcos) of integer
        
        x: array(barcos, sectores) of mpvar
    end-declarations
    
    
    costo::[130, 30, 510, 30, 340, 20,
            460, 150, 20, 40, 30, 450,
            40, 370, 120, 390, 40, 30]
    
    tiempo::[30, 50, 10, 11, 13, 9, 
             10, 20, 60, 10, 10, 17,
             70, 10, 10, 15, 8, 12]
    
    b::[50, 50, 50]
    
    costo_total := sum(i in barcos, j in sectores) costo(i, j) * x(i, j)
    
    forall(j in sectores) do 
        r1(j) := sum(i in barcos) x(i, j) = 1
    end-do
    
    
    forall(i in barcos) r2(i) := sum(j in sectores) tiempo(i, j) * x(i, j) <= b(i)
    
    forall(i in barcos, j in sectores) do 
        x(i, j) is_binary
    end-do
        
    
    minimize(costo_total)
    
    writeln("costo_total = ", getobjval)
    
    forall(i in barcos) do
        writeln
        forall(j in sectores) do
            write(getsol(x(i, j)), " ")
        end-do
    end-do
    
    writeln
    forall(i in barcos) do
        writeln
        write("Barco(", i,") => ")
        forall(j in sectores | getsol(x(i, j)) > 0.95) do
            write(j, " ")
        end-do
    end-do
    
    writeln
    writeln("\nBarco\tHolgura")
    forall(i in barcos) do
        writeln(i, "\t", b(i) - sum(j in sectores) tiempo(i, j) * getsol(x(i, j)))
    end-do
    
            

end-model
