model "prototype-example-networks"
    uses "mmxprs"; 
    
    declarations
        n = 6
        m = 14
        vertices =  1..n
        edges = 1..m
        
        demand: array(vertices) of real
        
        A_sparse: array(edges, 1..3) of real
        A_dense: array(vertices, vertices) of real
        
        x: dynamic array(vertices, vertices) of mpvar
  
    end-declarations
    
    demand::[150, 200, -130, -130, 0, 0]
    A_sparse::[1, 5, 8,
               1, 6, 13,
               1, 3, 25,
               1, 4, 28,
               2, 5, 15,
               2, 6, 12,
               2, 3, 26,
               2, 4, 25,
               5, 3, 16,
               5, 4, 17,
               6, 3, 14,
               6, 4, 16,
               5, 6, 6,
               6, 5, 6]
    
    forall(k in edges) do
        A_dense(round(A_sparse(k, 1)), round(A_sparse(k, 2))) := A_sparse(k, 3)
    end-do
    
    writeln
    write("\t")
    forall(j in vertices) do
       write(j, "\t")
    end-do
    forall(i in vertices) do
        writeln
        write(i, "\t")
        forall(j in vertices) do
            write(A_dense(i, j), "\t")
        end-do
    end-do
    writeln
    
    
    forall(i in vertices, j in vertices | (i = j and demand(i) > 0) or A_dense(i, j) <> 0) do
        create(x(i, j))
    end-do
    
   
    forall(i in vertices) do
        if demand(i) > 0 then
            res1(i) := sum(j in vertices) x(i, j) = demand(i)
        elif demand(i) < 0 then
            res1(i) := sum(j in vertices) x(j, i) = - demand(i)
        else
            res1(i) := sum(j in vertices) x(j, i) = sum(j in vertices) x(i, j)
        end-if
    end-do
    
    objetive := sum(i in vertices, j in vertices) x(i,j) * A_dense(i, j)
    
    minimize(objetive)
    
    
    writeln
    write("\t")
    forall(j in vertices) do
       write(j, "\t")
    end-do
    forall(i in vertices) do
        writeln
        write(i, "\t")
        forall(j in vertices) do
            write(getsol(x(i, j)), "\t")
        end-do
    end-do
    writeln
    
    writeln
    writeln("objetive = ", getobjval)
    
end-model
