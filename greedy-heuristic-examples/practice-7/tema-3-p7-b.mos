model ""
	uses "mmxprs"; !gain access to the Xpress-Optimizer solver
	uses "mmsystem"

	declarations
		p_init: 		integer
		p_max: 			integer
		k_max: 			integer
		n: 				integer
	 	m: 				integer
	 	dc: 			integer
	 	N_max:			integer
	end-declarations

	initializations from "aint5.dat"
		p_init
		p_max
		k_max
		N_max
		n
		m
		dc
	end-initializations

	declarations
		p_range 		= p_init..p_max
			
		servicios 		= 1..n
		demandas 		= 1..m

		d:				array(demandas, servicios) of integer
		h:				array(demandas) of integer

		!Greedy Vars
		cubierta: 		array(demandas) of integer
		solu:			array(servicios) of integer
		partial_sums:	array(servicios) of integer
		
		!Random Greedy Vars
		k_iter      	= 1..k_max
		k_list:			array(k_iter) of integer
		k_marca:		array(servicios) of integer

		N_iter			= 1..N_max
		cubierta_copy:	array(demandas) of integer
		solu_copy:		array(servicios) of integer
		
		!Xpress Vars
		x:				array(servicios) of mpvar
		z:				array(demandas) of mpvar
		
	end-declarations


	initializations from "aint5.dat"
		h
		d
	end-initializations
	
	h_total:=sum(i in demandas) h(i)


	!Xpress Solution Constant Restrictions
	!################################################################

	dem_cub:=sum(i in demandas)
		h(i) * z(i)
	
	forall(i in demandas)
		rescub(i):= sum(j in servicios | d(i,j) <= dc)
			x(j) >= z(i)
	
	
	forall(j in servicios)
		x(j)is_binary

	forall(i in demandas)
		z(i)is_binary
		
		
	!Solving
	!################################################################
	
	forall(p in p_range)do
		writeln("p = ", p)
		
		! Greedy Solution
		!############################################################
		
		n_cub := 0
		while(n_cub < p) do
			forall(j in servicios | solu(j) = 0) do
				partial_sums(j):= sum(i in demandas | d(i,j) <= dc and cubierta(i) = 0) h(i)
			end-do
			
			aux:=-999
			selected:=-1
			forall(j in servicios | solu(j) = 0) do
				if(aux < partial_sums(j)) then
					aux := partial_sums(j)
					selected := j
				end-if
			end-do
			
			solu(selected) := 1
			forall(i in demandas | d(i,selected) <= dc)
					cubierta(i):=1
					
			n_cub := n_cub + 1
		end-do
		
		writeln("\tgreedy solution: ")
		
		percent := 100 * sum(i in demandas|cubierta(i) = 1) h(i) / h_total
		writeln("\tpercent: ", percent, " %")
		write("\topened: ")
		forall(j in servicios | solu(j) = 1) 
			write(j,"  ")
		writeln("\n")
		
		
		!RESET Matrices
		forall(i in demandas) do
			cubierta(i) := 0
			cubierta_copy(i):=0
		end-do
		forall(j in servicios) do
			solu(j):=0
			solu_copy(j) := 0
		end-do
		
		
		
		! Random Greedy Solution
		!############################################################
		
		forall(n_i in N_iter) do
		
			forall(i in demandas) do
				cubierta(i) := 0
			end-do
			forall(j in servicios) do
				solu(j):=0
			end-do
			
			n_cub := 0
			while(n_cub < p) do
				forall(j in servicios | solu(j) = 0) do
					partial_sums(j):= sum(i in demandas | d(i,j) <= dc and cubierta(i) = 0) h(i)
				end-do
				
				forall(j in servicios) k_marca(j):=0
				forall(k in k_iter) do
					aux := -99
					forall(j in servicios | (solu(j) = 0 and k_marca(j) = 0)) do
						if(aux < partial_sums(j)) then
							aux := partial_sums(j)
							jmax := j
						end-if
					end-do
					k_marca(jmax):=1
					k_list(k):=jmax
				end-do
				
				k_win := ceil(random*k_max)
				selected := k_list(k_win)
				
				
				solu(selected) := 1
				forall(i in demandas | d(i,selected) <= dc)
						cubierta(i):=1
						
				n_cub := n_cub + 1
			end-do
			if(sum(i in demandas|cubierta_copy(i) = 1) h(i) < sum(i in demandas|cubierta(i) = 1) h(i)) then
				forall(j in servicios) do
			 		solu_copy(j) := solu(j)
				end-do
				forall(i in demandas) do
					cubierta_copy(i) := cubierta(i)
				end-do
			 end-if
		end-do
		
		forall(i in demandas) do
			cubierta(i) := cubierta_copy(i)
		end-do
		
		forall(j in servicios) do
			solu(j) := solu_copy(j)
		end-do
		
		writeln("\tgreedy random solution: ")
		
		percent := 100 * sum(i in demandas|cubierta(i) = 1) h(i) / h_total
		writeln("\tpercent: ", percent, " %")
		write("\topened: ")
		forall(j in servicios | solu(j) = 1) 
			write(j,"  ")
		writeln("\n")
		
		
		!RESET Matrices
		forall(i in demandas) do
			cubierta(i) := 0
		end-do
		forall(j in servicios) do
			solu(j):=0
		end-do
		
		! Xpress Solution
		!############################################################
		
		rest:= sum(j in servicios) 
			x(j) = p
			
		maximize(dem_cub)
		writeln("\txpress solution: ")
		percent := 100 * getobjval / h_total
		writeln("\tpercent: ", percent, " %")
		write("\topened: ")
		forall(j in servicios | x(j).sol = 1)
			write(j, "  ")
		writeln("\n")
		
	end-do
end-model
