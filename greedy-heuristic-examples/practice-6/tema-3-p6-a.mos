model "Actividades-recursos"
	uses "mmxprs"
	
	declarations
		n: 			integer
	 	m: 			integer
	 	dc_min:		integer
	 	dc_max:		integer
	end-declarations

	initializations from "tema-3-p6.dat"
		n
		m
		dc_min
		dc_max
	end-initializations
	
	declarations
		servicios	= 1..n
		demandas 	= 1..m
		
		dist:		array(demandas,servicios) of integer
		dc_iter 	= dc_min..dc_max
		a:			array(demandas,servicios) of integer
		
		
		cubierta:	array(demandas) of integer
		w: 			array(demandas) of integer
		d:			array(servicios) of integer 
		solu:		array(servicios) of integer
		fijada:		array(servicios) of integer
	end-declarations

			
	initializations from "tema-3-p6.dat"
		dist
	end-initializations
	
	
	declarations
		x:array(servicios)of mpvar
	end-declarations
	
	ganancia:=sum(j in servicios)
		x(j)
			
	forall(j in servicios)
		x(j) is_binary
		
			
			
			
	forall(dc in dc_iter) do
	
		writeln("dc = ", dc)
		
		forall(i in demandas, j in servicios) do
			if(dist(i,j) <= dc) then a(i,j):=1
			else a(i,j) := 0
			end-if
		end-do
		
		! Greedy Solution
		!############################################################
		
		forall(i in demandas) do
			cubierta(i) := 0
			w(i) := 0
		end-do
		
		forall(j in servicios) do
			d(j) := 0
			solu(j) := 0
			fijada(j) := 0
		end-do
		
		
		nfilcub := 0
		while(nfilcub < m) do
		
			forall(j in servicios | fijada(j) = 0) do
				d(j) := 0
				forall(i in demandas) do
					if(cubierta(i) = 0 and a(i,j) = 1) then
						d(j) := d(j) + 1
					end-if
				end-do
				
				if(d(j) = 0) then
					fijada(j):= 1
					solu(j) := 0
				end-if
	
			end-do
			
			aux := -99
			forall(j in servicios | fijada(j) = 0) do
				if(d(j) > aux) then
					aux := d(j)
					jmax := j
				end-if
			end-do
			solu(jmax) := 1
			fijada(jmax) := 1
			
			forall(i in demandas) do
				if(cubierta(i) = 0 and a(i, jmax) = 1) then
					cubierta(i) := 1
					nfilcub := nfilcub +1
				end-if
			end-do
			
			! ELIMINACION DE COLUMNAS COMPLETADAS
			forall(i in demandas) do
				w(i):=0
				forall(j in servicios) do
					if(solu(j)=1 and a(i,j)=1)then 
						w(i):=w(i)+1
					end-if
				end-do
			end-do
			forall(k in servicios | solu(k)=1) do
				elimin:= 1
				forall(i in demandas) do
					if(w(i) <1+a(i,k))then
						elimin :=0
					end-if
				end-do
				if(elimin=1) then 
					solu(k) :=0
					forall(i in demandas)w(i):=w(i)-a(i,k)
				end-if
			end-do
		end-do
		
		writeln("\tgreedy solution: ")
		writeln("\tinstalations: ", sum(j in servicios) solu(j))
		write("\topened: ")
		forall(j in servicios | solu(j) = 1) write(j,"  ")
		writeln("\n")
	
	
		! Xpress Solution
		!############################################################
		
		forall(i in demandas)
			rec(i):= sum(j in servicios | a(i,j) = 1) 
				x(j) >= 1
		minimize(ganancia)
		
		writeln("\txpress solution: ")
		writeln("\tinstalations: ", getobjval)
		write("\topened: ")
		forall(j in servicios | x(j).sol = 1) write(j,"  ")
		writeln("\n")
	end-do
end-model