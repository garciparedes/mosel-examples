model "Actividades-recursos"
	
	
	declarations
		n: integer
	 	m: integer
	end-declarations

	initializations from "tema-3-p6.dat"
		n
		m
	end-initializations
	
	declarations
		servicios	= 1..n
		demandas 	= 1..m
		dist:		array(demandas,servicios) of integer
		d:			array(servicios) of integer 
		cubierta: 	array(demandas) of integer
		solu,fijada:array(servicios) of integer
		a:			array(demandas,servicios) of integer
		dc 			= 80
	end-declarations

			
	initializations from "tema-3-p6.dat"
		dist
	end-initializations
	
	
	forall(i in demandas, j in servicios) do
		if(dist(i,j) <= dc) then a(i,j):=1
		else a(i,j) := 0
		end-if
	end-do
	
	
	nfilcub := 0
	while(nfilcub < m) do
		forall(j in servicios | fijada(j) = 0) do
			d(j) := 0
			forall(i in demandas) do
				if(cubierta(i) = 0 and a(i,j) = 1) then
					d(j) := d(j) + 1
				end-if
			end-do
			
			if(d(j) = 0) then
				fijada(j):= 1
				solu(j) := 0
			end-if

		end-do
		
		aux := -99
		forall(j in servicios | fijada(j) = 0) do
			if(d(j) > aux) then
				aux := d(j)
				jmax := j
			end-if
		end-do
		solu(jmax) := 1
		fijada(jmax) := 1
		
		forall(i in demandas) do
			if(cubierta(i) = 0 and a(i, jmax) = 1) then
				cubierta(i) := 1
				nfilcub := nfilcub +1
			end-if
		end-do
	end-do
	
	writeln("solution: ")
	writeln("instalations: \t", sum(j in servicios) solu(j))
	write("opened: \t")
	forall(j in servicios | solu(j) = 1) write(j,"\t")
	
end-model