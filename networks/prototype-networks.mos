model "prototype-example-networks"
    uses "mmxprs"; 
    
    declarations
        n = 6
        m = 14
        vertices =  1..n
        edges = 1..m
        
        demand: array(vertices) of real
        
        A_sparse: array(edges, 1..3) of real
        A_dense: array(vertices, vertices) of real
        
        x: dynamic array(vertices, vertices) of mpvar
        
        y_in: array(vertices) of mpvar
        y_out: array(vertices) of mpvar
    end-declarations
    
    demand::[-150, -200, 130, 130, 0, 0]
    A_sparse::[1, 5, 8,
               1, 6, 13,
               1, 3, 25,
               1, 4, 28,
               2, 5, 15,
               2, 6, 12,
               2, 3, 26,
               2, 4, 25,
               5, 3, 16,
               5, 4, 17,
               6, 3, 14,
               6, 4, 16,
               5, 6, 6,
               6, 5, 6]
    
    total_demand := 0.0
    total_offer := 0.0
    
    forall(i in vertices) do
        if demand(i) > 0 then
            total_demand := total_demand + demand(i)
        elif demand(i) < 0 then
            total_offer := total_offer - demand(i)
        end-if
    end-do
    
    writeln("total_demand = ", total_demand)
    writeln("total_offer = ", total_offer)
    
    forall(k in edges) do
        A_dense(round(A_sparse(k, 1)), round(A_sparse(k, 2))) := A_sparse(k, 3)
    end-do
    
    forall(i in vertices) do
        writeln
        forall(j in vertices) do
            write(A_dense(i, j), "\t")
        end-do
    end-do
    writeln
    
    
    forall(i in vertices, j in vertices | (i = j and demand(i) < 0) or A_dense(i, j) <> 0) do
        create(x(i, j))
    end-do
    
    forall(j in vertices) do
        res1(j) := sum(i in vertices) x(j, i) = y_out(j)
        res2(j) := sum(i in vertices) x(i, j) = y_in(j)
    end-do
   
    forall(i in vertices) do
        if demand(i) > 0 then
            res3(i) := y_in(i) = demand(i)
        elif demand(i) < 0 then
            res3(i) := y_out(i) = - demand(i)
        else
            res3(i) := y_in(i) = y_out(i)
        end-if
    end-do
    
    objetive := sum(i in vertices, j in vertices) x(i,j) * A_dense(i, j)
    
    minimize(objetive)
    
    
    writeln
    write("\t")
    forall(j in vertices) do
       write(j, "\t")
    end-do
    forall(i in vertices) do
        writeln
        write(i, "\t")
        forall(j in vertices) do
            write(getsol(x(i, j)), "\t")
        end-do
    end-do
    writeln
    
    writeln
    writeln("objetive = ", getobjval)
    
end-model
