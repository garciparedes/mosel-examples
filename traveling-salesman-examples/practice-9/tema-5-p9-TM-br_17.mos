model ""
	uses "mmxprs";
	setparam("XPRS_MAXTIME", 60)

	declarations
		file_name 	= "br17.dat"
		n:			integer
	end-declarations

	initializations from file_name
		n
	end-initializations

	declarations
		nodes 		= 1..n
		d:			array(nodes, nodes) of integer
		x:			array(nodes, nodes) of mpvar
		u:			array(nodes) of mpvar
	end-declarations

	initializations from file_name
		d
	end-initializations

	forall(i in nodes)
		res_j(i) := sum(j in nodes) x(i,j) = 1

	forall(j in nodes)
		res_i(j) := sum(i in nodes) x(i,j) = 1

	obj := sum(i, j in nodes)
		d(i,j) * x(i,j)

	forall(i, j in nodes)
		x(i,j) is_binary

	forall(i in nodes | i <> 1) do
		res_u_down(i) := 2 <= u(i)
		res_u_up(i) := u(i) <= n
	end-do
	res_u := u(1) = 1


	forall(i, j in nodes | i <> 1 and j <> 1)
		res_MTZ(i, j) := (u(i) - u(j) +1) <= ((n-1)*(1 - x(i,j)))

	minimize(obj)


	t := 1
	counts := 0
	distance := 0.0
	write("path: ")
	while(counts < n) do
		forall(j in nodes | x(t,j).sol = 1) do
			if(counts < n) then
				write(t, " ")
				distance := distance +d(t,j)
				t := j
				counts := counts + 1
			end-if
		end-do
	end-do
	writeln("\ndistance: ", distance)


end-model
